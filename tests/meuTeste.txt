programa {
   flut real, imaginario, x, y, xtemp;
   flut realCentro, imaginarioCentro, escalaZoom;
   int iteracao, maxIteracoes;
   int largura, altura;
   int i, j;

   int indice;
   car simbolos[5];

   /* Configurações de Zoom */
   realCentro = -0.5; /* Centro do zoom no eixo real */
   imaginarioCentro = 0.0; /* Centro do zoom no eixo imaginário */
   escalaZoom = 0.9; /* Fator de zoom */

   /* Configurações da tela */
   largura = 160;
   altura = 46;
   maxIteracoes = 100;

   /* Definição de símbolos para diferentes níveis de divergência */
   simbolos[4] = '#';
   simbolos[3] = '&';
   simbolos[2] = '+';
   simbolos[1] = '.';
   simbolos[0] = ' ';

   i = 0;
   enquanto (i < altura) execute {
      j = 0;
      enquanto (j < largura) execute {
         real = (j - largura / 2.0) * (4.0 / largura) * escalaZoom + realCentro;
         imaginario = (i - altura / 2.0) * (4.0 / altura) * escalaZoom + imaginarioCentro;
         x = 0.0;
         y = 0.0;
         iteracao = 0;
         enquanto (x * x + y * y <= 4.0 e iteracao < maxIteracoes) execute {
            xtemp = x * x - y * y + real;
            y = 2.0 * x * y + imaginario;
            x = xtemp;
            iteracao = iteracao + 1;
         }
         indice = iteracao * 5 / maxIteracoes; /* Ajusta o índice baseado no número de iterações */
         se (indice > 4) entao
            indice = 4;
         escreva simbolos[indice];
         j = j + 1;
      }
      novalinha;
      i = i + 1;
   }
}
